; Juggling Turtles
; Program written by Stan Munson (trollopite@hotmail.com).


TO SETUP
MAKE "DELAY 1
MAKE "PATHLEFT [[-50 -50] [-25 40] [0 50] [25 40] [50 -50]]
MAKE "PATHRIGHT [[50 -50] [25 40] [0 50] [-25 40] [-50 -50]]
MAKE "LEFTHAND [1 2]
MAKE "RIGHTHAND [3]
SETTURTLES 4
TELL 0 PENUP HT SETY -95 SETTSIZE 4 ST
TELL [1 2 3] HT PU LOCKSHAPE SETTSIZE 1.5
SETSHAPE "|~HOME/TOOLBOX/OTHER PICTURES/BALLRED|
ASK 1 [SETPC "RED SETXY FIRST :PATHLEFT ST]
ASK 2 [SETPC "BLUE SETXY FIRST :PATHLEFT ST]
ASK 3 [SETPC "GREEN SETXY FIRST :PATHRIGHT ST]
IGNORE LAUNCH [JUGGLE]
END

TO JUGGLE
WHILE ["TRUE] [
TOSSLEFT
TOSSRIGHT]
END

TO TOSSLEFT
MAKE "TOSS FIRST :LEFTHAND
MAKE "LEFTHAND BUTFIRST :LEFTHAND
MAKE "RIGHTHAND LPUT :TOSS :RIGHTHAND
ASK 0 [SETSHAPE "|~HOME/TOOLBOX/JUGGLING/TOSSLEFT|]
MOVE :TOSS :PATHLEFT "FALSE
WHILE [.LT ASK :TOSS [YCOR] 45] []
END

TO TOSSRIGHT
MAKE "TOSS FIRST :RIGHTHAND
MAKE "RIGHTHAND BUTFIRST :RIGHTHAND
MAKE "LEFTHAND LPUT :TOSS :LEFTHAND
ASK 0 [SETSHAPE "|~HOME/TOOLBOX/JUGGLING/TOSSRIGHT|]
MOVE :TOSS :PATHRIGHT "FALSE
WHILE [.LT ASK :TOSS [YCOR] 45] []
END

TO MORE
LOCAL "SHAPE
MAKE "SHAPE 0
TELL [1 2 3]
WHILE ["TRUE] [
MAKE "SHAPE 1 + REMAINDER :SHAPE 18
SETSHAPE WORD "|~HOME/TOOLBOX/JUGGLING/JUGGLE| :SHAPE]
END

TO MOVE :TURTLE :PATH :FOREVER?
IF MOVING? :TURTLE THEN STOP.MOVING :TURTLE
PPROP :TURTLE "MOVE.PATH :PATH
PPROP :TURTLE "MOVE.FOREVER? :FOREVER?
PPROP :TURTLE "MOVE.ID LAUNCH LIST "MOVEMENT QUOTE (:TURTLE)
END

TO MOVING? :TURTLE
OUTPUT NOT EMPTY? GPROP :TURTLE "MOVE.ID
END

TO STOP.MOVING :TURTLE
IF NOT MOVING? :TURTLE THEN STOP
LOCAL "ID
MAKE "ID GPROP :TURTLE "MOVE.ID
REMPROP :TURTLE "MOVE.PATH
REMPROP :TURTLE "MOVE.FOREVER?
REMPROP :TURTLE "MOVE.ID
(HALT :ID)
END

TO MOVEMENT :TURTLE
(LOCAL "POINT "MAXPOINTS "NEXTPOINT "PATH "PENSTATE)
MAKE "PATH GPROP :TURTLE "MOVE.PATH
MAKE "MAXPOINTS COUNT :PATH
MAKE "POINT :MAXPOINTS
ASK :TURTLE [
MAKE "PENSTATE LIST PEN PENCOLOR
PENUP SETXY FIRST :PATH
SETPEN :PENSTATE]
WHILE ["TRUE] [
MAKE "POINT 1 + REMAINDER :POINT :MAXPOINTS 
MAKE "NEXTPOINT ITEM :POINT :PATH 
ASK :TURTLE [SETH TOWARDS :NEXTPOINT] 
WHILE [NOT EQUAL? ASK :TURTLE [GETXY] :NEXTPOINT] [
ASK :TURTLE [FD 1 WAIT :DELAY]] 
  IF END.OF.MOVING? THEN STOP.MOVING :TURTLE STOP 
]
END

TO END.OF.MOVING?
OUTPUT AND (EQUAL? :POINT :MAXPOINTS) (EQUAL? "FALSE GPROP :TURTLE "MOVE.FOREVER?)
END

CS CT SETUP
PRINT [|Click the Stop Light to stop juggling.|]
PRINT [|Type MORE to see more juggling tricks.|]
